; 安装一个新的int 09H中断历程。
; 功能：在DOS下，按"A"(通码：1E)键后，除非不再松开，如果松开，就显示满屏的“A”，其他的键照常处理。 

ASSUME CS:CODE, SS:STACK

STACK SEGMENT
	DB 128 DUP (0)
STACK ENDS

CODE SEGMENT
	START:
	MOV AX, CS 
	MOV DS, AX
	MOV AX, STACK
	MOV SS, AX
	MOV SP, 0080H
	MOV AX, 0000H
	MOV ES, AX
	
	; 安装int 09H中断处理程序
	MOV SI, OFFSET INT09H
	MOV DI, 0204H
	MOV CX, OFFSET INT09H_END - OFFSET INT09H
	CLD
	REP MOVSB
	; 保存原来的int 09H中断向量
	PUSH ES:[09H * 4]
	POP ES:[0200H]
	PUSH ES:[09H * 4 + 2]
	POP ES:[0202H]	
	; 设置新的int 09H中断向量
	MOV WORD PTR ES:[09H * 4], 0204H
	MOV WORD PTR ES:[09H * 4 + 2], 0000H
	; 程序返回
	MOV AX, 4C00H
	INT 21H

	INT09H:
	PUSH AX
	PUSH BX
	PUSH CX
	PUSH ES
	
	MOV AX, 0000H
	MOV ES, AX
	IN AL, 60H
	PUSHF
	CALL DWORD PTR ES:[0200H]
	CMP AL, 1EH + 80H
	JNZ INT09H_RET
	
	MOV AX, 0B800H
	MOV ES, AX
	MOV BX, 0000H 
	
	MOV CX, 2000
	INT09H_S:
	MOV BYTE PTR ES:[BX], 41H	
	ADD BX, 0002H
	LOOP INT09H_S
	

	INT09H_RET:
	POP ES
	POP CX
	POP BX
	POP AX
	IRET
	INT09H_END:
	NOP

CODE ENDS
	END START
