; 安装一个新的int 7ch中断例程，为显示输出提供如下功能子程序
; (1) 清屏；
; (2) 设置前景色；
; (3) 设置背景色；
; (4) 向上滚动一行
; 入口参数说明如下
; (1) 用ah寄存器传递功能号：0 表示清屏，1表示设置前景色，2 表示设置背景色，3 表示向上滚动一行
; (2) 对于1/2号功能，用al传送颜色值，al={0, 1, 2, ,3 ,4, 5, 6, 7}

ASSUME CS:CODE

CODE SEGMENT
	START:
	; 测试功能
	; CALL CLEAR_SCREEN	
	; MOV AL, 03H
	; CALL SET_FOREGROUND_COLOR
	; MOV AL, 03H
	; CALL SET_BACKGROUND_COLOR
	; CALL SCROLL_UP_ONE_LINE

	; MOV AH, 02H
	; MOV AL, 03H
	; CALL SET_SCREEN

	; 安装中断处理程序int 7ch
	MOV AX, CS
	MOV DS, AX
	MOV AX, 0000H
	MOV ES, AX
	MOV SI, OFFSET INT7CH
	MOV DI, 0200H
	CLD
	MOV CX, OFFSET INT7CH_END - OFFSET INT7CH
	REP MOVSB
	; 设置中断向量表
	MOV WORD PTR ES:[7CH * 4], 0200H
	MOV WORD PTR ES:[7CH * 4 + 2], 0000H
	; 测试int 7ch 
	MOV AH, 01H
	MOV AL, 03H
	INT 7CH

	; 程序返回
	MOV AX, 4C00H
	INT 21H
	
	INT7CH:

	SET_SCREEN:
	PUSH BX
	JMP SHORT SET_SCRREN_START
	FUNCTION_ADDRESS	DW	0200H + CLEAR_SCREEN - OFFSET SET_SCREEN
				DW	0200H + SET_FOREGROUND_COLOR - OFFSET SET_SCREEN
				DW	0200H + SET_BACKGROUND_COLOR - OFFSET SET_SCREEN
				DW	0200h + SCROLL_UP_ONE_LINE - OFFSET SET_SCREEN

	SET_SCRREN_START:
	CMP AH, 03H
	JA SET_SCREEN_RET
	MOV BL, AH
	MOV BH, 00H
	ADD BX, BX
	ADD BX, 0200H + FUNCTION_ADDRESS - OFFSET SET_SCREEN
	CALL WORD PTR CS:[BX]

	SET_SCREEN_RET:
	POP BX
	IRET
	
	; 0 清屏
	CLEAR_SCREEN:
	PUSH AX
	PUSH CX
	PUSH BX
	PUSH ES

	MOV AX, 0B800H
	MOV ES, AX
	MOV BX, 0000H
	
	MOV CX, 07D0H; 2000
	CLEAR_SCREEN_S:
	MOV BYTE PTR ES:[BX], ' '
	ADD BX, 0002H
	LOOP CLEAR_SCREEN_S

	POP ES
	POP BX
	POP CX
	POP AX
	RET

	; 1 设置前景色
	SET_FOREGROUND_COLOR:
	PUSH BX
	PUSH CX
	PUSH ES

	MOV BX, 0B800H
	MOV ES, BX
	MOV BX, 0000H
	
	MOV CX, 07D0H; 2000
	SET_FOREGROUND_COLOR_S:
	AND BYTE PTR ES:[BX].01H, 0F8H
	OR ES:[BX].01H, AL 
	ADD BX, 0002H
	LOOP SET_FOREGROUND_COLOR_S

	POP ES
	POP CX
	POP BX
	RET
	
	; 2 设置背景色
	SET_BACKGROUND_COLOR:
	PUSH BX
	PUSH CX
	PUSH ES

	MOV BX, 0B800H
	MOV ES, BX
	MOV BX, 0000H
	
	MOV CL, 04H
	SHL AL, CL

	MOV CX, 07D0H; 2000
	SET_BACKGROUND_COLOR_S:
	AND BYTE PTR ES:[BX].01H, 8FH
	OR ES:[BX].01H, AL 
	ADD BX, 0002H
	LOOP SET_BACKGROUND_COLOR_S

	POP ES
	POP CX
	POP BX
	RET

	; 3 向上滚动一行
	SCROLL_UP_ONE_LINE:
	PUSH AX
	PUSH SI
	PUSH DI
	PUSH DS
	PUSH ES
	
	MOV AX, 0B800H
	MOV DS, AX
	MOV ES, AX
	MOV SI, 00A0H
	MOV DI, 0000H
	CLD

	MOV CX, 24 * 00A0H
	REP MOVSB	
;	MOV CX, 0018H
;	SCROLL_UP_ONE_LINE_S:
;	PUSH CX
;	MOV CX, 00A0H
;	REP MOVSB
;	ADD SI, 00A0H
;	ADD DI, 00A0H
;	POP CX
;	LOOP SCROLL_UP_ONE_LINE_S

	POP ES
	POP DS
	POP DI
	POP SI
	POP AX
	RET
	
	INT7CH_END:
	NOP	
CODE ENDS
	END START
