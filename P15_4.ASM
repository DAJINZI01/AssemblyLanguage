; 编程：在屏幕中间依次显示'a' - 'z'，并可以让人看清。在显示的过程中，按下ESC键后，改变显示的颜色

ASSUME CS:CODE, DS:DATA, SS:STACK

DATA SEGMENT
	INT09H_ADDR	DW 0000H, 0000H
DATA ENDS

STACK SEGMENT
	DB 128 DUP (0)
STACK ENDS

CODE SEGMENT
	START:
	MOV AX, DATA
	MOV DS, AX
	MOV AX, STACK
	MOV SS, AX
	MOV SP, 0080H

	; 保存原来的int 09H中断向量
	MOV AX, 0000H
	MOV ES, AX
	MOV BX, OFFSET INT09H_ADDR
	MOV AX, ES:[09H * 4]
	MOV [BX], AX
	MOV AX, ES:[09H * 4 + 2]
	MOV [BX].02H, AX
	; 设置新的int 09H中断向量
	CLI
	MOV WORD PTR ES:[09H * 4], OFFSET INT09H
	MOV ES:[09H * 4 + 2], CS 
	STI
	

	MOV AX, 0B800H
	MOV ES, AX
	MOV BX, 12 * 0A0H + 50H
	
	MOV AL, 61H
	A_Z:
	MOV ES:[BX], AL
	MOV BYTE PTR ES:[BX].01H, 03H
	CALL DELAY
	INC AL
	CMP AL, 7AH
	JNA A_Z

	; 恢复原来的int 09H中断向量
	MOV AX, 0000H
	MOV ES, AX
	PUSH DS:[0000H]
	POP ES:[09H * 4]
	PUSH DS:[0002H]
	POP ES:[09H * 4 + 2]

	MOV AX, 4C00H
	INT 21H

	INT09H:
	PUSH AX
	PUSH BX
	PUSH ES
	
	IN AL, 60H
	PUSHF
	CALL DWORD PTR DS:[0000H]
	CMP AL, 01H
	JNZ INT09H_RET
	
	MOV AX, 0B800H
	MOV ES, AX
	MOV BX, 12 * 0A0H + 50H
	INC BYTE PTR ES:[BX].01H	

	INT09H_RET:
	POP ES
	POP BX
	POP AX
	IRET
	

	DELAY:
	PUSH AX
	PUSH DX

	MOV DX, 0010H
	MOV AX, 0000H
	DELAY_S:
	SUB AX, 0001H
	SBB DX, 0000H
	CMP AX, 0000H
	JNZ SHORT DELAY_S 
	CMP DX, 0000H
	JNZ SHORT DELAY_S 

	POP DX
	POP AX
	RET

CODE ENDS
	END START
